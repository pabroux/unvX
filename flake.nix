{
  description = "unvX: a simple Home Manager configuration";

  inputs = {
    # Determinate Nix deprecates the default loading of nixpkgs, home-manager,
    # and flake-utils from the global flake registry, encouraging explicit input
    # specification instead
    nixpkgs.url = "github:NixOS/nixpkgs/nixpkgs-unstable";
    nixpkgs-stable.url = "github:NixOS/nixpkgs/nixos-25.11";
    flake-utils.url = "github:numtide/flake-utils";
    home-manager = {
      url = "github:nix-community/home-manager";
      inputs.nixpkgs.follows = "nixpkgs";
    };
  };

  outputs = {
    nixpkgs,
    nixpkgs-stable,
    home-manager,
    flake-utils,
    ...
  }: let
    # unvX.nix is a file generated by the `install` script. unvX.nix contains
    # information about your unvX installation (e.g. your unvX module path on
    # your machine)
    unvX = import ./unvX.nix;
  in
    flake-utils.lib.eachSystem flake-utils.lib.allSystems (system: let
      pkgs = nixpkgs.legacyPackages.${system};
      pkgs-stable = nixpkgs-stable.legacyPackages.${system};
    in {
      # Specify your Home Manager profiles. Home Manager will by default match
      # first `$USER@$(hostname)`. If not found, it will then default to `$USER`
      packages.homeConfigurations = {
        "sulfyderz@Sulfyderz-MacBook-Pro.local" = home-manager.lib.homeManagerConfiguration {
          pkgs = pkgs;

          # Specify your Home Manager configurations
          modules = [./host/Sulfyderz-MacBook-Pro/user/sulfyderz/home.nix];

          # Specify extra arguments to pass through your configurations
          extraSpecialArgs = {inherit unvX;};
        };
      };

      # Specify your development shell with tools you want for CI/CD (optional).
      # This is currenlty used by `pre-commit`
      devShells.default = pkgs-stable.mkShell {
        buildInputs = with pkgs-stable; [
          alejandra
          gitleaks
          gitlint
          pre-commit
          shfmt
          stylua
          yamlfmt
        ];
      };
    });
}
